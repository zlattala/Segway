timed csp SegwayRPEvents
csp-begin
	SegwayRPEvents = {| 
		Segway::angle.in,
		Segway::gyroX.in,
		Segway::gyroY.in,
		Segway::gyroZ.in,
		Segway::leftMotorVelocity.in,
		Segway::rightMotorVelocity.in,
		Segway::setLeftMotorSpeedCall,
		Segway::setRightMotorSpeedCall,
		Segway::disableInterruptsCall,
		Segway::enableInterruptsCall
	|}
csp-end

// define range from -maxAngle to maxAngle and its complement
timed csp AngleRange
csp-begin
	AngleRange = { -2..2 } 
csp-end
timed csp OutsideAngleRange
csp-begin
	OutsideAngleRange = diff(core_real, AngleRange)
csp-end

timed csp AngleInRange
csp-begin
	AngleInRange = { Segway::angle.in.x | x <- AngleRange}
csp-end

timed csp AngleOutsideRange
csp-begin
	AngleOutsideRange = { Segway::angle.in.x | x <- OutsideAngleRange}
csp-end

// When RotationPID::D is set to two, 
// after the first (rotationUpdate-1) times setRightMotorSpeed() has occurred,
// if values greater than or equal to -maxAngle and less than or equal to maxAngle are communicated by the event angle,
// the next value passed to setRightMotorSpeed() is twice the value communicated by gyroZ.
// This repeats every rotationUpdate calls to setRightMotorSpeed().
timed csp RotationPIDProp3
csp-begin
	Timed(OneStep) {
		RotationPIDProp3 = RotationPIDProp3Count(0)
		
		RotationPIDProp3Count(lcount) =
			-- events other than setting right motor speed are unconstrained
			([] e : diff(SegwayRPEvents, {|Segway::setRightMotorSpeedCall|}) @ e -> RotationPIDProp3Count(lcount))
			 []
			-- allow setLeftMotorSpeedCall to occur with any values and update the count for the first rotationUpdate-2 times
			(lcount < const_BalanceSTM_rotationUpdate-2) & Segway::setRightMotorSpeedCall!0 -> RotationPIDProp3Count(lcount+1)
			 []
			-- allow setRightMotorSpeedCall to occur with any values for the (rotationUpdate-1)th time,
			-- then go to check angle since we are into the first part of the precondition
			(lcount >= const_BalanceSTM_rotationUpdate-2) & Segway::setRightMotorSpeedCall?x -> RotationPIDProp3AngleCheck(0)
			
		RotationPIDProp3AngleCheck(gyroz) =
			-- allow any events other than getting gyroZ, setting right speed and checking the angle
			([] e : diff(SegwayRPEvents, {|Segway::angle, Segway::gyroZ, Segway::setRightMotorSpeedCall|})
					@ e -> RotationPIDProp3AngleCheck(gyroz))
			 []
			-- record value from gyroZ
			Segway::gyroZ.in?gz -> RotationPIDProp3AngleCheck(gz)
			 []
			-- if setRightMotorSpeedCall occurs without an angle being set, the precondition is broken so the values are not constrained, then we return to the beginning
			Segway::setRightMotorSpeedCall?x -> RotationPIDProp3
			 []
			-- if angle is in AngleRange, go to the restricted state, passing the current recorded values of gyroZ
			Segway::angle.in?x : AngleRange -> RotationPIDProp3Restrict(gyroz)
			 []
			-- if angle is outside AngleRange, the precondition does not hold, so continue
			Segway::angle.in?x : OutsideAngleRange -> RotationPIDProp3AngleCheck(gyroz)
			
		RotationPIDProp3Restrict(gyroz) =
			-- allow any events other than getting gyroZ and setting right speed (ignore further occurrences of angle)
			([] e : diff(SegwayRPEvents, {|Segway::gyroZ, Segway::setRightMotorSpeedCall|}) @ e -> RotationPIDProp3Restrict(gyroz))
			 []
			-- record value from gyroZ
			Segway::gyroZ.in?gz -> RotationPIDProp3Restrict(gz)
			 []
			-- setRightMotorSpeedCall must occur with a value equal to the sum of the recorded speeds, then we return to the beginning
			Segway::setRightMotorSpeedCall!Mult(2, gyroz, core_real) -> RotationPIDProp3
	}
csp-end
timed assertion RotationPIDProp3_refines: Segway refines RotationPIDProp3
	in the traces model
	with constant ^P of AnglePID    set to 0,
	     constant D  of AnglePID    set to 0,
	     constant ^P of SpeedPID    set to 0,
	     constant I  of SpeedPID    set to 0 and
	     constant D  of RotationPID set to 2