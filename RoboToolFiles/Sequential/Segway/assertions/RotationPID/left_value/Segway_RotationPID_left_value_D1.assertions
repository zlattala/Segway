timed csp SegwayRPEvents
csp-begin
	SegwayRPEvents = {| 
		Segway::angle.in,
		Segway::gyroX.in,
		Segway::gyroY.in,
		Segway::gyroZ.in,
		Segway::leftMotorVelocity.in,
		Segway::rightMotorVelocity.in,
		Segway::setLeftMotorSpeedCall,
		Segway::setRightMotorSpeedCall,
		Segway::disableInterruptsCall,
		Segway::enableInterruptsCall
	|}
csp-end

// define range from -maxAngle to maxAngle and its complement
timed csp AngleRange
csp-begin
	AngleRange = { -2..2 } 
csp-end
timed csp OutsideAngleRange
csp-begin
	OutsideAngleRange = diff(core_real, AngleRange)
csp-end

timed csp AngleInRange
csp-begin
	AngleInRange = { Segway::angle.in.x | x <- AngleRange}
csp-end

timed csp AngleOutsideRange
csp-begin
	AngleOutsideRange = { Segway::angle.in.x | x <- OutsideAngleRange}
csp-end

// When RotationPID::D is set to one, 
// after the first (rotationUpdate-1) times setLeftMotorSpeed() has occurred,
// if values greater than or equal to -maxAngle and less than or equal to maxAngle are communicated by the event angle,
// the next value passed to setLeftMotorSpeed() is the negation of the value communicated by gyroZ.
// This repeats every rotationUpdate calls to setLeftMotorSpeed().
timed csp RotationPIDProp2
csp-begin
	Timed(OneStep) {
		RotationPIDProp2 = RotationPIDProp2Count(0)
		
		RotationPIDProp2Count(lcount) =
			-- events other than setting motor speeds are unconstrained
			([] e : diff(SegwayRPEvents, {|Segway::setLeftMotorSpeedCall|}) @ e -> RotationPIDProp2Count(lcount))
			 []
			-- allow setLeftMotorSpeedCall to occur with any values and update the count for the first rotationUpdate-2 times
			(lcount < const_BalanceSTM_rotationUpdate-2) & Segway::setLeftMotorSpeedCall!0 -> RotationPIDProp2Count(lcount+1)
			 []
			-- allow setLeftMotorSpeedCall to occur with any values for the (rotationUpdate-1)th time,
			-- then go to check angle since we are into the first part of the precondition
			(lcount >= const_BalanceSTM_rotationUpdate-2) & Segway::setLeftMotorSpeedCall?x -> RotationPIDProp2AngleCheck(0)
			
		RotationPIDProp2AngleCheck(gyroz) =
			-- allow any events other than getting gyroZ, setting left speed and checking the angle
			([] e : diff(SegwayRPEvents, {|Segway::angle, Segway::gyroZ, Segway::setLeftMotorSpeedCall|})
					@ e -> RotationPIDProp2AngleCheck(gyroz))
			 []
			-- record value from gyroZ
			Segway::gyroZ.in?gz -> RotationPIDProp2AngleCheck(gz)
			 []
			-- if setLeftMotorSpeedCall occurs without an angle being set, the precondition is broken so the values are not constrained, then we return to the beginning
			Segway::setLeftMotorSpeedCall?x -> RotationPIDProp2
			 []
			-- if angle is in AngleRange, go to the restricted state, passing the current recorded values of gyroZ
			Segway::angle.in?x : AngleRange -> RotationPIDProp2Restrict(gyroz)
			 []
			-- if angle is outside AngleRange, the precondition does not hold, so continue
			Segway::angle.in?x : OutsideAngleRange -> RotationPIDProp2AngleCheck(gyroz)
			
		RotationPIDProp2Restrict(gyroz) =
			-- allow any events other than getting gyroZ and setting left speed (ignore further occurrences of angle)
			([] e : diff(SegwayRPEvents, {|Segway::gyroZ, Segway::setLeftMotorSpeedCall|}) @ e -> RotationPIDProp2Restrict(gyroz))
			 []
			-- record value from gyroZ
			Segway::gyroZ.in?gz -> RotationPIDProp2Restrict(gz)
			 []
			-- setLeftMotorSpeedCall must occur with a value equal to the sum of the recorded speeds, then we return to the beginning
			Segway::setLeftMotorSpeedCall!(-gyroz) -> RotationPIDProp2
	}
csp-end
timed assertion RotationPIDProp2_refines: Segway refines RotationPIDProp2
	in the traces model
	with constant ^P of AnglePID    set to 0,
	     constant D  of AnglePID    set to 0,
	     constant ^P of SpeedPID    set to 0,
	     constant I  of SpeedPID    set to 0 and
	     constant D  of RotationPID set to 1