timed csp constrain
csp-begin
	constrain(x, min, max) = if x < min then min else if x > max then max else x
csp-end

timed csp SegwayRPEvents
csp-begin
	SegwayRPEvents = {| 
		Segway::angle.in,
		Segway::gyroX.in,
		Segway::gyroY.in,
		Segway::gyroZ.in,
		Segway::leftMotorVelocity.in,
		Segway::rightMotorVelocity.in,
		Segway::setLeftMotorSpeedCall,
		Segway::setRightMotorSpeedCall,
		Segway::disableInterruptsCall,
		Segway::enableInterruptsCall
	|}
csp-end

// define range from -maxAngle to maxAngle and its complement
timed csp AngleRange
csp-begin
	AngleRange = { -2..2 } 
csp-end
timed csp OutsideAngleRange
csp-begin
	OutsideAngleRange = diff(core_real, AngleRange)
csp-end

timed csp AngleInRange
csp-begin
	AngleInRange = { Segway::angle.in.x | x <- AngleRange}
csp-end

timed csp AngleOutsideRange
csp-begin
	AngleOutsideRange = { Segway::angle.in.x | x <- OutsideAngleRange}
csp-end

// When SpeedPID::P is set to one and SpeedPID::I is set to two (and all other PID constants are zero), 
// after the first (speedUpdate-1) times setRightMotorSpeed() has occurred,
// the next value passed to setRightMotorSpeed()
//		when the value communicated by angle is greater than or equal to -maxAngle and less than or equal to maxAngle
// is passed to setRightMotorSpeed()
//		whenever the value communicated by angle is greater than or equal to -maxAngle and less than or equal to maxAngle
//		until speedUpdate further occurrences of setRightMotorSpeed() (after the initial three) have happened, after which it repeats (from "the next value passed...")
timed csp SpeedPIDProp7
csp-begin
	Timed(OneStep) {
		SpeedPIDProp7 = SpeedPIDProp7Count(0)
		
		-- SpeedPIDProp7Count represents the state when fewer than speedUpdate motor settings have occurred and we count the Right motor settings
		SpeedPIDProp7Count(count) =
			-- allow any events other than setting Right speed
			([] e : diff(SegwayRPEvents, {|Segway::setRightMotorSpeedCall|}) @ e -> SpeedPIDProp7Count(count))
			 []
			-- allow setRightMotorSpeedCall to occur with any values and update the count for the first speedUpdate-2 times
			(count < const_BalanceSTM_speedUpdate-2) & Segway::setRightMotorSpeedCall?x -> SpeedPIDProp7Count(count+1)
			 []
			-- allow setRightMotorSpeedCall to occur with any values for the (speedUpdate-1)th time,
			-- then we reset the count and check the angle
			(count == const_BalanceSTM_speedUpdate-2) & Segway::setRightMotorSpeedCall?x -> SpeedPIDProp7AngleCheck(0)
			
		-- SpeedPIDProp7AngleCheck represents the state in which we are waiting for the angle to be in range before we can record the setRightMotorSpeed() value
		SpeedPIDProp7AngleCheck(count) =
			-- allow any events other than setting Right speed and getting angle
			([] e : diff(SegwayRPEvents, {|Segway::angle, Segway::setRightMotorSpeedCall|}) @ e -> SpeedPIDProp7AngleCheck(count))
			 []
			-- allow setRightMotorSpeedCall to occur with any values, since we haven't had an angle in range, and update the count mod speedUpdate
			Segway::setRightMotorSpeedCall?x -> SpeedPIDProp7AngleCheck(if count < const_BalanceSTM_speedUpdate-1 then count+1 else 0)
			 []
			-- if the angle is out of range, continue
			Segway::angle.in?x : OutsideAngleRange -> SpeedPIDProp7AngleCheck(count)
			 []
			-- if the angle is in range, go to a state where we can record the next value of setRightMotorSpeedCall
			Segway::angle.in?x : AngleRange -> SpeedPIDProp7RecordValue(count)
			
		-- SpeedPIDProp7RecordValue represents the state in which angle is in range and we are waiting to record the setRightMotorSpeed() value
		SpeedPIDProp7RecordValue(count) =
			-- allow any events other than setting Right speed and getting angle
			([] e : diff(SegwayRPEvents, {|Segway::angle, Segway::setRightMotorSpeedCall|}) @ e -> SpeedPIDProp7RecordValue(count))
			 []
			-- if an angle in range occurs, continue
			Segway::angle.in?x : AngleRange -> SpeedPIDProp7RecordValue(count)
			 []
			-- if an angle out of range occurs, return to checking for an angle in range
			Segway::angle.in?x : OutsideAngleRange -> SpeedPIDProp7AngleCheck(count)
			 []
			-- if this isn't the speedUpdate-th occurrence, when setRightMotorSpeedCall occurs, record the value and increment the count
			(count < const_BalanceSTM_speedUpdate-1) & Segway::setRightMotorSpeedCall?x -> SpeedPIDProp7InRange(count+1, x)
			 []
			-- if this is the speedUpdate-th occurrence of setRightMotorSpeedCall, go back to checking the angle to start a new cycle
			(count == const_BalanceSTM_speedUpdate-1) & Segway::setRightMotorSpeedCall?x -> SpeedPIDProp7AngleCheck(0)
			
		-- SpeedPIDProp7InRange represents the state where the angle is in range and the same value is repeatedly output for setRightMotorSpeed()
		SpeedPIDProp7InRange(count, value) =
			-- allow any events other than setting Right speed and getting angle
			([] e : diff(SegwayRPEvents, {|Segway::angle, Segway::setRightMotorSpeedCall|}) @ e -> SpeedPIDProp7InRange(count, value))
			 []
			-- allow setRightMotorSpeedCall to occur outputting value and update the count for the first speedUpdate-1 times
			(count < const_BalanceSTM_speedUpdate-1) & Segway::setRightMotorSpeedCall!value -> SpeedPIDProp7InRange(count+1, value)
			 []
			-- allow setRightMotorSpeedCall to occur outputting value for the speedUpdate-th time, then discard the value and reset the count
			(count == const_BalanceSTM_speedUpdate-1) & Segway::setRightMotorSpeedCall!value -> SpeedPIDProp7AngleCheck(0)
			 []
			-- if angle is in range, continue
			Segway::angle.in?x : AngleRange -> SpeedPIDProp7InRange(count, value)
			 []
			-- if angle is out of range, switch to have no bounds on the setRightMotorSpeed() value, but keep value recorded
			Segway::angle.in?x : OutsideAngleRange -> SpeedPIDProp7OutOfRange(count, value)
		
		-- SpeedPIDProp7InRange represents the state where the angle is out of range and setRightMotorSpeed() is unrestricted
		SpeedPIDProp7OutOfRange(count, value) =
			-- allow any events other than setting Right speed and getting angle
			([] e : diff(SegwayRPEvents, {|Segway::angle, Segway::setRightMotorSpeedCall|}) @ e -> SpeedPIDProp7OutOfRange(count, value))
			 []
			-- allow setRightMotorSpeedCall to occur with any values, since we haven't had an angle in range, and update the count
			(count < const_BalanceSTM_speedUpdate-1) & Segway::setRightMotorSpeedCall?x -> SpeedPIDProp7OutOfRange(count+1, value)
			 []
			-- allow setRightMotorSpeedCall to occur with any values, since we haven't had an angle in range, and reset the count and discard the value
			(count == const_BalanceSTM_speedUpdate-1) & Segway::setRightMotorSpeedCall?x -> SpeedPIDProp7AngleCheck(0)
			 []
			-- if angle is out of range, continue
			Segway::angle.in?x : OutsideAngleRange -> SpeedPIDProp7OutOfRange(count, value)
			 []
			-- if angle is in range, switch to constrain setRightMotorSpeed() value
			Segway::angle.in?x : AngleRange -> SpeedPIDProp7InRange(count, value)
	}
csp-end
timed assertion SpeedPIDProp7_refines: Segway refines SpeedPIDProp7
	in the traces model
	with constant ^P of AnglePID    set to 0,
	     constant D  of AnglePID    set to 0,
	     constant ^P of SpeedPID    set to 1,
	     constant I  of SpeedPID    set to 2 and
	     constant D  of RotationPID set to 0