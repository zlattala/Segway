timed csp SegwayRPEvents
csp-begin
	SegwayRPEvents = {| 
		Segway::angle.in,
		Segway::gyroX.in,
		Segway::gyroY.in,
		Segway::gyroZ.in,
		Segway::leftMotorVelocity.in,
		Segway::rightMotorVelocity.in,
		Segway::setLeftMotorSpeedCall,
		Segway::setRightMotorSpeedCall,
		Segway::disableInterruptsCall,
		Segway::enableInterruptsCall
	|}
csp-end
	     
// define range from -maxAngle to maxAngle and its complement
timed csp AngleRange
csp-begin
	AngleRange = { -2..2 } 
csp-end
timed csp OutsideAngleRange
csp-begin
	OutsideAngleRange = diff(core_real, AngleRange)
csp-end

timed csp AngleInRange
csp-begin
	AngleInRange = { Segway::angle.in.x | x <- AngleRange}
csp-end

timed csp AngleOutsideRange
csp-begin
	AngleOutsideRange = { Segway::angle.in.x | x <- OutsideAngleRange}
csp-end

// assertions with just angle control

// When AnglePID::P=1, speed setting is equal to angle when angle is in AngleRange
timed csp AnglePIDProp1
csp-begin
	Timed(OneStep) {
		-- allow any events when angle is outside AngleRange
		AnglePIDProp1 =
			([] e : diff(SegwayRPEvents, AngleInRange) @ e -> AnglePIDProp1)
			 []
			-- when angle is inside AngleRange, enter the restricted state, passing the angle value
			Segway::angle.in?x : AngleRange -> RestrictAnglePIDProp1(x)
			
		-- restrict motor speed setting to the value of the angle
		RestrictAnglePIDProp1(x) =
			([] e : diff(SegwayRPEvents, {|Segway::angle, Segway::setLeftMotorSpeedCall, Segway::setRightMotorSpeedCall|}) @ e -> RestrictAnglePIDProp1(x))
			 []
			Segway::setLeftMotorSpeedCall!x -> RestrictAnglePIDProp1(x)
			 []
			Segway::setRightMotorSpeedCall!x -> RestrictAnglePIDProp1(x)
			 []
			-- continue in the restricted state with new angle value when angle remains within AngleRange
			Segway::angle.in?y : AngleRange -> RestrictAnglePIDProp1(y)
			 []
			-- leave restricted state when angle is outside AngleRange
			Segway::angle.in?y : OutsideAngleRange -> AnglePIDProp1
	}
csp-end
timed assertion AnglePIDProp1_refines: Segway refines AnglePIDProp1
	in the traces model
	with constant ^P of AnglePID    set to 1, // this assertion applies when only the P AnglePID component is active
	     constant D  of AnglePID    set to 0,
	     constant ^P of SpeedPID    set to 0,
	     constant I  of SpeedPID    set to 0 and
	     constant D  of RotationPID set to 0     